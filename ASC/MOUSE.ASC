10 rem ********************************
11 rem *                              *
12 rem *       MOUSE ACCESSORY        *
13 rem *                              *
14 rem *        By  F. Lionet         *
15 rem *    Part of the Stos Basic    *
16 rem *    (c) 1988 Jawx/Mandarin    *
17 rem *                              *
18 rem ********************************
19 rem
50 dim C(15)
55 YH=2 : YD=22 : CO=1
100 key off : curs off : DM=mode : for X=0 to 15 : C(X)=colour(X) : next X
105 if mode=0 then mode 1
110 if mode=2 then 150
115 windopen 1,10,0,60,5,1 : curs off : locate 0,1 : centre "LOAD A DEGAS PI1 PICTURE" : set zone 1,xgraphic(0),ygraphic(0) to xgraphic(57)+8,ygraphic(2)+8
120 windopen 2,10,6,60,5,1 : curs off : locate 0,1 : centre "LOAD A DEGAS PI2 PICTURE" : set zone 2,xgraphic(0),ygraphic(0) to xgraphic(57)+8,ygraphic(2)+8
125 windopen 3,10,12,60,5,1 : curs off : locate 0,1 : centre "LOAD A NEOCHROME PICTURE" : set zone 3,xgraphic(0),ygraphic(0) to xgraphic(57)+8,ygraphic(2)+8
130 goto 155
150 windopen 4,10,12,60,5,1 : curs off : locate 0,1 : centre "LOAD A DEGAS PI3 PICTURE" : set zone 4,xgraphic(0),ygraphic(0) to xgraphic(57)+8,ygraphic(2)+16
155 windopen 5,10,20,60,5,1 : curs off : locate 0,1 : centre "QUIT" : set zone 5,xgraphic(0),ygraphic(0) to xgraphic(57)+8,ygraphic(2)+16/divy
200 OLDZ=0 : repeat 
205 Z=zone(0)
210 if Z=OLDZ then 230
215 if OLDZ then qwindow OLDZ : border 1
220 if Z then qwindow Z : border 2
225 OLDZ=Z
230 until mouse key<>0 and Z<>0
235 if OLDZ then qwindow OLDZ : border 1
300 if Z=5 then default : end 
305 if Z=1 then FILTER$=".PI1" : M=0
310 if Z=2 then FILTER$=".PI2" : M=1
315 if Z=3 then FILTER$=".NEO" : M=0
320 if Z=4 then FILTER$=".PI3" : M=2
325 F$=file select$("*"+FILTER$,"     Please select picture",10)
330 if F$="" then bell : goto 200
335 if instr(F$,FILTER$)=0 then bell : goto 200
350 if M<>mode then mode M else cls
355 hide on : load F$ : show on 
365 YGH=ygraphic(YH) : YGD=ygraphic(YD) : TY=16/divy
370 H$=screen$(back,0,YGH to 640/divx,YGH+TY)
375 D$=screen$(back,0,YGD to 640/divx,YGD+TY)
380 COMAX=16 : if mode=1 then COMAX=4
400 CO=1 : OLDXM=-1 : auto back off 
405 repeat : XM=x mouse : YM=y mouse : KM=mouse key : until KM or XM<>OLDXM or YM<>OLDYM
410 if y mouse>200/divy then Y=YH else Y=YD
415 if OLDY<>Y then cls back,0,0,YGH to 640/divx,YGH+TY : cls back,0,0,YGD to 640/divx,YGD+TY : screen$(back,0,YGH)=H$ : screen$(back,0,YGD)=D$ : screen copy back to logic : locate 0,Y : centre space$(20) : OLDY=Y
420 paper 0 : pen CO : locate 0,Y : centre " X:"+str$(x mouse)+"  Y:"+str$(y mouse)+" "
425 if KM=2 and mode<>2 then while mouse key : wend : CO=(CO+1) mod COMAX : XM=-1
430 OLDXM=XM : OLDYM=YM : wait vbl : if KM<>1 then 405
435 if mode=0 then mode 1 else cls
440 for X=0 to 15 : colour X,C(X) : next X
445 goto 100
