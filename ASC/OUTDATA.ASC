1 rem ****************************
2 rem *                          *
3 rem *      DATA GENERATOR      *
4 rem *                          *
5 rem *  Part of the STOS basic  *
6 rem *    By Fran‡ois Lionet    *
7 rem * (c) 1988  Jawx/Mandarin  *
8 rem *                          *
9 rem **********15/8/1988*********
10 key off : if mode=0 then mode 1
15 for X=1 to 15 : erase X : next X
99 rem ~~~> Loads bank
100 F$=file select$("*.MBK","    Enter the name of the bank         to be listed",2)
105 if F$="" then bell : default : end 
110 if instr(F$,".")=0 then F$=F$+".MBK"
115 if instr(F$,".MBK")=0 then bell : goto 100
120 cls : locate 0,12 : centre "... LOADING ..." : on error goto 150
125 load F$
130 on error goto 0
135 goto 200
150 boom : cls : locate 1,12 : centre "DISK ERROR !" : wait 75 : resume 155
155 on error goto 0
160 goto 100
199 rem ~~~> Screen or printer
200 cls
205 locate 1,10 : input "(S)creen or (P)rinter ?";A$ : A$=upper$(A$)
210 PRT=-1 : if A$="S" then PRT=0
215 if A$="P" then PRT=1
220 if PRT<0 then 200
225 cls
230 clear key 
299 rem ~~~> Listing
300 for BN=1 to 15 : if length(BN)=0 then next BN
305 A$=" ---> Bank number :"+str$(BN) : gosub 1000
310 A$=" ---> Bank length :"+str$(length(BN)) : gosub 1000
315 A$="" : gosub 1000
400 A$="-----------------------------------------------------------------" : gosub 1000
405 A$="| Adrs |                     Datas                      | Check |" : gosub 1000
410 A$="-----------------------------------------------------------------" : gosub 1000
415 AD=start(BN) : ADEND=start(BN)+length(BN)
420 repeat 
430 A$="|      |                                                |       |"
435 B$=hex$(AD-start(BN),4)-"$"
440 mid$(A$,3)=B$
445 CHK=0 : for N=0 to 7 : if AD>=ADEND then B$=" 0000" : goto 465
450 D=peek(AD) : for X=1 to 3 : if D=peek(AD+X) then next X : goto 460
455 B$=" "+hex$(deek(AD),4)-"$" : CHK=CHK+deek(AD) : AD=AD+2 : goto 465
460 X=1 : repeat : inc X : until peek(AD+X)<>D or X=254 or AD+X>=ADEND : X=X and $FFFFFFFE : CHK=CHK+D*256+X+$10000 : AD=AD+X : B$="*"+(hex$(D,2)-"$")+(hex$(X,2)-"$")
465 mid$(A$,9+N*6)=B$ : mid$(A$,59)=hex$(CHK,5)-"$"
470 next N : gosub 1000
475 until AD>=ADEND
480 A$="-----------------------------------------------------------------" : gosub 1000
485 for X=1 to 15 : erase X : next X
490 default : end 
1000 print A$
1005 if PRT then on error goto 1100 : lprint A$ : on error goto 0
1010 if inkey$<>"" then wait key 
1015 return 
1100 boom : print : print "Printer error! : clearkey : wait key"
1105 default : end 
