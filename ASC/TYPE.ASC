1 rem
2 rem TYPE.ACB
3 rem
4 rem STOS basic accessory
5 rem By F. Lionet & C. Sotiropoulos
6 rem (c) JAWX - MANDARIN
7 rem
50 on error goto 500
100 restore 1000+language*100
105 for X=1 to 3 : read A$ : menu$ (X)=A$ : next X
110 for X=1 to 3 : read N : for Y=1 to N : read A$,Z : menu$ (X,Y)=A$ : if Z=0 then menu$ (X,Y) off 
115 next Y : next X : read ECR$,IMP$,ERR1$,ERR2$,T1$,T2$,T3$,T4$ : gosub 310
120 menu on 
121 on menu goto 200,300,400
130 on menu on : print : print T4$
135 goto 135
199 rem---> File
200 on mnselect gosub 210,205,220
205 goto 130
210 N$=file select$("*.*") : return 
220 default : end 
299 rem---> Output
300 on mnselect gosub 310,315
305 goto 130
310 OUT=0 : menu$ (2,1)=ECR$+"<---" : menu$ (2,2)=IMP$ : return 
315 OUT=1 : menu$ (2,1)=ECR$ : menu$ (2,2)=IMP$+"<---" : return 
400 rem---> Start printing
405 clear key : print : print "=====> ";N$ : print : print T1$ : print : print T2$ : wait key : clw 
415 open in #1,N$
420 repeat : A$=input$(#1,1) : if asc(A$)<32 and asc(A$)<>13 and asc(A$)<>10 then A$="."
425 if OUT then lprint A$; else print A$;
430 K$=inkey$ : until eof(1) or K$<>""
435 if eof(1) then 450
440 if K$=chr$(13) then 450
445 wait key : goto 420
450 close : wait 25 : print : print : print T3$ : bell : goto 130
499 rem---> Errors
500 close : wait 25 : boom : if errn=10 then print : print ERR2$ : print : resume 130
505 print : print ERR1$ : print : resume 130
1000 data " File "," Output "," Start printing "
1005 data 3," Choose file ",1,"-------------",0," Quit",1
1010 data 2," Screen ",1," Printer ",1
1015 data 1," Start printing ",1
1020 data " Screen "," Printer "," Disk error !!!","Printer is not ready!","<any key> stops and restart listing, <RETURN> ends listing.","Press any key to start..."
1025 data "Printing finished...","Choose one option..."
1100 data " Fichier "," Affichage "," Debut impression "
1105 data 3," Choisir fichier",1,"-----------------",0," Sortir",1
1110 data 2," Ecran ",1," Imprimante ",0
1115 data 1," Debut impression ",1
1120 data " Ecran"," Imprimante "," Erreur de disque !!!","L'imprimante n'est pas prete!","<space> arrete et redemmare le listing, <RETURN> arrete definitivement.","Appuyer sur une touche pour demmarer..."
1125 data "Impression terminee...","Choisissez une option..."
