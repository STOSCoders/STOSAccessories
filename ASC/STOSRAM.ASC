10 rem -----------------------------
11 rem    RAM DISK UTILITY  V 1.1
12 rem
13 rem         By F. Lionet
14 rem    (c) 1989 Jawx/Mandarin
15 rem -----------------------------
50 dim L(40),D(40),N$(40)
55 DR$="A"
60 SIZE=150 : FOLD$="A:\COMPILER"
65 LRAM=1333
100 key off : if mode=0 then mode 1
105 clw : curs off : hide on 
110 locate 0,0 : square 79,3,3
115 centre "STOS RAM DISK"
120 locate 0,8 : centre "Drive selected "+DR$
121 print : print : centre space$(79) : centre "Ram disk size"+str$(SIZE)+" KBytes"
122 print : print : centre space$(79) : if FOLD$<>"" then centre "Folder to copy : "+FOLD$
123 locate 0,18 : centre "Press <A> or <B> to select drive" : print : centre "<S> to set ram disc size" : print : centre "<C> to set automatic copy" : print : centre "<G> to save ram-disc" : print : print : centre "Any other key ends."
125 A$=upper$(inkey$) : if A$="" then 125
130 if A$="A" then DR$="A" : goto 120
135 if A$="B" then DR$="B" : goto 120
140 for X=8 to 23 : locate 0,X : centre space$(40) : next X
145 if A$="S" then 200
150 if A$="C" then 300
155 if A$="G" then 400
160 default : end 
200 locate 5,13 : input "Please enter ram disc size (in KBytes) :";A$
205 X=val(A$) : if X<12 or X>$200000 then bell : goto 100
210 SIZE=X : goto 100
300 locate 5,10 : centre "Please enter PATHNAME of folder to copy," : print : centre "press <RETURN> if no copy wanted." : print : print : centre "Note: this folder must be in the root directory."
305 locate 20,17 : input "Pathname : ";A$
310 FOLD$=upper$(A$) : if A$="" then 100
315 if mid$(FOLD$,2,1)<>":" then FOLD$="" : goto 100
320 if mid$(FOLD$,3,1)<>"\" then FOLD$=left$(FOLD$,2)+"\"+mid$(FOLD$,3)
325 if right$(FOLD$,1)="\" then FOLD$=left$(FOLD$,len(FOLD$)-1)
330 if len(FOLD$)>12+3 or len(FOLD$)<=3 then bell : FOLD$=""
335 goto 100
400 locate 5,10 : centre "Please insert a disk into drive "+DR$ : locate 0,20 : centre "Press any key." : clear key : wait key : centre space$(79) : locate 0,10 : centre space$(79)
405 NCOP=0
410 on error goto 900
415 A$=dir first$("*.*",-1) : drive$=DR$ : dir$="\" : A$=dir first$("*.*",-1)
500 if dir first$("AUTO",-1)<>"" then 600
505 locate 0,10 : centre "Creating AUTO folder."
510 mk dir "AUTO"
600 dir$="\AUTO" : LL=0
605 A$=dir first$("*.*",-1) : goto 611
610 A$=dir next$ : if A$="" then 625
611 if A$="" then 625
615 if val(mid$(A$,42))<>0 then 610
616 if left$(A$,12)-" "="STOSRAM.PRG" then 610
620 N$(NCOP)=left$(A$,12) : L(NCOP)=val(mid$(A$,13,8)) : LL=LL+L(NCOP) : inc NCOP : goto 610
625 if NCOP=0 then 700
630 if LL>free-$1000 then 910
635 erase 10 : reserve as work 10,LL : AD=start(10)
640 for N=0 to NCOP-1 : locate 0,10 : centre "        Loading : "+N$(N)+"         "
645 D(N)=AD : bload N$(N),AD : AD=AD+L(N) : next N
650 for N=0 to NCOP-1 : locate 0,10 : centre "        Killing : "+N$(N)+"         "
655 kill N$(N) : next N
700 AD=start(11)+$20 : loke AD,SIZE : loke AD+4,(SIZE+9)*1024
705 AD=AD+8 : if FOLD$="" then loke AD,0 else for X=1 to len(FOLD$) : poke AD,asc(mid$(FOLD$,X,1)) : inc AD : next X : poke AD,0
710 locate 0,10 : centre space$(79) : centre "Saving STOSRAM.PRG"
715 bsave "STOSRAM.PRG",start(11) to start(11)+LRAM
720 if NCOP=0 then 800
725 for N=0 to NCOP-1 : locate 0,10 : centre "        Saving : "+N$(N)+"         "
730 bsave N$(N),D(N) to D(N)+L(N) : next N
800 centre space$(79) : inverse on : centre " JOB DONE " : inverse off 
805 locate 0,22 : centre "Press any key"
810 dir$="\" : clear key : wait key 
815 goto 100
900 bell : locate 0,10 : centre space$(79) : centre "DISK ERROR" : resume 905
905 locate 0,20 : centre "Press any key" : clear key : wait key : goto 100
910 bell : locate 0,10 : centre space$(79) : centre "OUT OF MEMORY" : resume 905
