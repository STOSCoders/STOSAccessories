1 rem ********************************
2 rem *                              *
3 rem *      PICTURE COMPACTOR       *
4 rem *                              *
5 rem *     For  the Stos Basic      *
6 rem *     By  Francois Lionet      *
7 rem *                              *
8 rem *     (c) JAWX / MANDARIN      *
9 rem *                              *
10 rem********************************
50 C0=colour(0) : C1=colour(1) : hide : curs off : if mode=0 then colour 1,C0 : mode 1 : OLDM=1
55 FLAGS=%10 : AFF=1
99 rem---> Initialise menus
100 restore 50000+language*100
105 for X=1 to 3 : read A$ : menu$ (X)=A$ : next X
110 for X=1 to 3 : read N : for Y=1 to N : read A$,Z : menu$ (X,Y)=A$ : if Z=0 then menu$ (X,Y) off 
115 next Y : next X
120 if accnb=0 then menu$ (1,4) off 
145 on menu goto 2000,3000,4000
150 menu on 4 : menu freeze : curs off : colour 0,C0 : colour 1,C1 : show 
199 rem---> Initialise text
200 restore 51000+language*100 : dim T$(50)
205 X=0 : repeat : read T$(X) : inc X : until T$(X-1)=""
299 rem---> Reserves memory bank
300 erase 5 : reserve as screen 5
305 reserve as work 6,38000
399 rem---> Check extension
400 cls 5 : on error goto 410
405 X=pack(5,6) : goto 500
410 T=11 : N=1 : gosub 10000
415 resume 2205
499 rem---> Reads data packing
500 dim HAUT(10),L(3),H(3) : restore 52000
505 for X=1 to 8 : read HAUT(X) : next X
510 for X=0 to 2 : read L(X),H(X) : next X
1000 on error goto 0
1005 colour 0,C0 : colour 1,C1 : if mode=0 then mode 1
1010 curs off : locate 20,8 : square 40,7,3 : cdown : cdown : centre T$(14)
1015 menu on : on menu on 
1020 goto 1020
1997 rem
1998 rem---> STOS
1999 rem
2000 menu freeze : on mnselect gosub 2100,2005,2200,2300
2005 goto 1000
2099 rem About Compactor
2100 windopen 1,13,5,54,16,6 : curs off 
2105 set paint 3,9,1 : ink 1 : bar xgraphic(0),ygraphic(0) to xgraphic(51)+8,ygraphic(13)+16/divy
2110 locate 0,2 : centre T$(0) : locate 0,4 : centre T$(1) : locate 0,7 : centre T$(2) : locate 0,9 : centre T$(3)
2115 locate 0,12 : centre T$(12)
2120 repeat : until mouse key
2125 while mouse key : wend 
2130 windel 1 : return 
2199 rem Quit
2200 gosub 10100 : if R=0 then return 
2205 if OLDM then colour 1,colour(0) : mode 0
2210 default : end 
2299 rem Quit and grab
2300 T=17 : N=2 : gosub 10000
2305 gosub 10100 : if R=0 then return 
2310 erase 5 : reserve as data 5,LPACK : copy start(6),start(6)+LPACK to start(5)
2315 erase 6
2320 put key "Bgrab"+str$(accnb)+",5 : Erase"+str$(accnb)+",5`"
2325 goto 2205
2997 rem
2998 rem---> DISK
2999 rem
3000 menu freeze : on mnselect gosub 3100,3200,3300,3400,3005,3500,3600
3005 goto 1000
3099 rem Load NEO picture
3100 paper 1 : pen 0 : R$=file select$("*.NEO",T$(8),3) : paper 0 : pen 1 : if R$="" then bell : return 
3105 if upper$(right$(R$,4))<>".NEO" then bell : return 
3110 AFF=0 : goto 3415
3200 paper 1 : pen 0 : R$=file select$("*.PI1",T$(9),3) : paper 0 : pen 1 : if R$="" then bell : return 
3205 if upper$(right$(R$,4))<>".PI1" then bell : return 
3210 AFF=0 : goto 3415
3300 paper 1 : pen 0 : R$=file select$("*.PI2",T$(9),3) : paper 0 : pen 1 : if R$="" then bell : return 
3305 if upper$(right$(R$,4))<>".PI2" then bell : return 
3310 AFF=1 : goto 3415
3400 paper 1 : pen 0 : R$=file select$("*.PI3",T$(9),3) : paper 0 : pen 1 : if R$="" then bell : return 
3405 if upper$(right$(R$,4))<>".PI3" then bell : return 
3410 AFF=2
3415 on error goto 3450
3420 menu$ (3,1) off : menu$ (3,2) off : windopen 1,20,9,40,7,5 : curs off : locate 0,2 : centre T$(19)
3425 load R$,5 : windel 1 : menu$ (3,1) on 
3430 if AFF=mode then hide : screen copy 5 to logic : get palette (5) : repeat : until mouse key : while mouse key : wend : screen copy back to logic : show : menu$ (3,2) on : return 
3435 if mode<>2 and AFF<>2 then hide : colour 1,C0 : mode 0 : screen copy 5 to logic : get palette (5) : repeat : until mouse key : while mouse key : wend : colour 0,C0 : cls logic : show : menu$ (3,2) on : return 
3440 T=20 : N=2 : gosub 10000 : return 
3450 boom : windel 1 : T=13 : N=1 : gosub 10000 : resume 3455
3455 return 
3499 rem Save BINARY file
3500 R$=file select$("*.PAC",T$(10),5) : if R$="" then bell : return 
3505 if instr(R$,".")=0 then R$=left$(R$,8)+".PAC"
3510 on error goto 3450
3515 windopen 1,20,9,40,7,5 : curs off : locate 0,2 : centre T$(19)
3520 bsave R$,start(6) to start(6)+LPACK
3525 windel 1 : return 
3599 rem Save MEMORY BANK
3600 R$=file select$("*.MBK",T$(22),5) : if R$="" then bell : return 
3605 if instr(R$,".MBK")=0 then bell : return 
3610 windopen 1,20,9,40,7,5 : curs off : locate 0,2 : centre T$(19)
3615 on error goto 3650
3620 hide : screen copy 5 to back : erase 5 : reserve as data 5,LPACK
3625 copy start(6),start(6)+LPACK to start(5) : save R$,5
3630 erase 5 : reserve as screen 5 : screen copy back to 5 : screen copy logic to back : show 
3635 windel 1 : return 
3650 gosub 3630 : boom : T=13 : N=1 : gosub 10000 : resume 3455
3997 rem
3998 rem---> PICTURE
3999 rem
4000 menu freeze : on mnselect gosub 4100,4200,4005,4400,4500
4005 goto 1000
4099 rem Pack whole picture
4100 windopen 1,10,6,60,15,2 : curs off : inverse on : clw 
4105 locate 0,1 : centre T$(23) : locate 0,3
4110 C=0 : Z=$8000 : for X=1 to 8
4115 Y=pack(5,6,AFF,FLAGS,HAUT(X),0,0,L(AFF),H(AFF)/HAUT(X))
4120 print "        ";T$(24);X;T$(25);Y;T$(28) : if Y<=Z then Z=Y : C=X
4125 next X : wait 100
4130 if C=0 then windel 1 : T=26 : N=1 : gosub 10000 : menu$ (1,4) off : menu$ (2,6) off : menu$ (2,7) off : return 
4135 inverse off : clw : locate 0,4 : centre T$(27)+str$(Z)+T$(28) : fix 2 : locate 0,6 : centre str$((Z/32080.0)*100)+" % "+T$(29)
4140 LPACK=pack(5,6,AFF,FLAGS,HAUT(C),0,0,L(AFF),H(AFF)/HAUT(C))
4145 locate 0,10 : centre T$(12) : gosub 10015
4150 menu$ (2,7) on : menu$ (2,6) on : if accnb then menu$ (1,4) on : return 
4155 return 
4199 rem Pack part of the picture
4200 T=30 : N=2 : gosub 10000
4205 if AFF<>mode then hide : colour 1,C0 : mode 0
4210 hide : screen copy 5 to back : screen copy back to logic : get palette (5) : show on 
4215 NC=2^(divx*divy)-1 : DX=0 : DY=0 : FX=640/divx : FY=400/divy : set line $FFFF,1,0,0
4220 C=1 : repeat : ink C : box DX,DY to FX-1,FY-1 : inc C : if C>NC then C=1
4225 MK=mouse key : K$=inkey$ : wait 5 : until MK or K$<>""
4230 screen copy 5 to back : screen copy 5 to logic
4235 if K$<>"" then 4275
4240 X=((x mouse+2)/16)*16 : Y=((y mouse+1)/2)*2 : if MK=2 then 4260
4245 if X<FX then DX=X
4250 if Y<FY-2 then DY=Y
4255 goto 4220
4260 if X>DX then FX=X
4265 if Y>DY+2 then FY=Y
4270 goto 4220
4275 colour 0,C0 : colour 1,C1 : if AFF=0 then mode 1 else cls
4280 windopen 1,10,6,60,15,2 : curs off : inverse on : clw 
4285 locate 0,1 : centre T$(23) : locate 0,3
4290 C=0 : Z=$8000 : for X=1 to 8
4295 D=FY-DY : if D mod HAUT(X)<>0 then 4300 else Y=pack(5,6,AFF,FLAGS,HAUT(X),DX/16,DY,(FX-DX)/16,D/HAUT(X)) : print "        ";T$(24);X;T$(25);Y;T$(28) : if Y<=Z then Z=Y : C=X
4300 next X : wait 100
4310 if C=0 then windel 1 : T=26 : N=1 : gosub 10000 : menu$ (1,4) off : menu$ (2,6) off : menu$ (2,7) off : return 
4315 inverse off : clw : locate 0,4 : centre T$(27)+str$(Z)+T$(28) : fix 2 : locate 0,6 : centre str$((Z/32080.0)*100)+" % "+T$(29)
4320 LPACK=pack(5,6,AFF,FLAGS,HAUT(C),DX/16,DY,(FX-DX)/16,D/HAUT(C))
4325 locate 0,10 : centre T$(12) : gosub 10015
4330 menu$ (2,7) on : menu$ (2,6) on : if accnb then menu$ (1,4) on : return 
4335 return 
4399 rem Erase picture  ON/OFF
4400 bchg 0,FLAGS : if btst(0,FLAGS) then menu$ (3,4)=T$(5) else menu$ (3,4)=T$(4)
4405 return 
4499 rem Change palette ON/OFF
4500 bchg 1,FLAGS : if btst(1,FLAGS) then menu$ (3,5)=T$(7) else menu$ (3,5)=T$(6)
4505 return 
9997 rem
9998 rem---> Alarm box
9999 rem
10000 windopen 1,10,7,60,10,3 : curs off : inverse on : clw 
10005 locate 0,6 : centre T$(12)
10010 for X=1 to N : locate 0,X*2 : centre T$(T+X-1) : next X
10015 repeat : until mouse key
10020 while mouse key : wend 
10025 windel 1
10030 return 
10097 rem
10098 rem---> Confirn
10099 rem
10100 windopen 1,20,9,20,7,2 : curs off : inverse on : clw : locate 0,2 : centre T$(15) : set zone 1,xgraphic(0),ygraphic(0) to xgraphic(17)+8,ygraphic(4)+16/divy
10105 windopen 2,40,9,20,7,2 : curs off : inverse on : clw : locate 0,2 : centre T$(16) : set zone 2,xgraphic(0),ygraphic(0) to xgraphic(17)+8,ygraphic(4)+16/divy
10110 repeat : until mouse key and (zone(0)=1 or zone(0)=2)
10115 if zone(0)=1 then R=1 else R=0 : bell 
10120 windel 1 : windel 2 : reset zone 1 : reset zone 2 : return 
49999 rem ---> English menus
50000 data " STOS "," DISK "," PICTURE "
50005 data 4," About COMPACT.ACB ",1,"-------------------",0," Quit",1," Quit and grab",0
50010 data 7," Load PICTURE.NEO",1," Load PICTURE.PI1",1," Load PICTURE.PI2",1," Load PICTURE.PI3",1,"------------------",0," Save BINARY FILE",0," Save MEMORY BANK",0
50015 data 5," Pack whole picture",0," Pack portion...",0,"--------------------",0," Erase screen",1,"*Change palette",1
50099 rem ---> Menu Francais
50100 data " STOS "," DISQUE "," IMAGE "
50105 data 4," COMPACTER !",1,"--------------------",0," Quitter",1," Quitter/r‚cup‚rer",1
50110 data 7," Charger IMAGE.NEO",1," Charger IMAGE.PI1",1," Charger IMAGE.PI2",1," Charger IMAGE.PI3",1,"--------------------",0," Sauver F. BINAIRE",0," Sauver B. MEMOIRE",0
50115 data 5," Compacter image",0," Compacter partie",0,"--------------------",0," Effacer l'‚cran",1,"*Changer palette",1
50999 rem---> English messages
51000 data "PICTURE COMPACTOR","Stos basic extension","By F. Lionet","(c) Jawx  / Mandarin 1988"
51005 data " Erase screen","*Erase screen"," Change palette","*Change palette"
51010 data "        LOAD NEO PICTURE","       LOAD DEGAS PICTURE","        SAVE BINARY FILE            (default extension: .PAC)"
51015 data "The COMPACT.EX extension is not loaded!","Press mouse key to go on..."
51020 data "Disk error!","Select an option...","CONFIRM","ABORT","QUIT and GRAB: WARNING!","packed picture is copied in bank 5!"
51025 data "A moment please...","This picture can't be displayed","with your monitor...","        SAVE MEMORY BANK                Bank number is 5!"
51030 data "Trying best way to pack picture:"," - Try"," ,size:","This picture is not worth packing!","Final size:"," bytes.","of the initial size."
51035 data "PACK PART OF THE PICTURE: use LEFT and RIGHT button","to fix corners, then press <SPACE> to pack.",""
51099 rem---> Messages Fran‡ais
51100 data "COMPACTEUR D'IMAGES","Extension au Stos basic","Par F. Lionet","(c) Jawx / Mandarin 1988"
51105 data " Effacer l'‚cran","*Effacer l'‚cran"," Changer palette","*Changer palette"
51110 data "     CHARGER UNE IMAGE NEO","     CHARGER UNE IMAGE DEGAS","   SAUVER L'IMAGE COMPACTEE      (Extension par defaut: .PAC)"
51115 data "L'extension COMPACT.EXA n'est pas charg‚e!","Pressez une touche de la souris pour continuer..."
51120 data "Erreur disquette!","Selectionnez une option...","CONFIRMER","ANNULER","QUITTER / RECUPERER: ATTENTION!","L'image compact‚e est recup‚r‚e dans la banque #5!"
51125 data "Un moment S.V.P...","Cette image ne peut-etre affich‚e","avec votre moniteur...","  SAUVER LA BANQUE DE MEMOIRE     La banque sauv‚e est la 5!"
51130 data "Je cherche la meilleure m‚thode..."," - Essai"," ,taille:","Cette image est trop compliqu‚e!","Taille finale:"," octets.","de la taille initiale."
51135 data "COMPACTER UNE PARTIE: Utilisez les boutons GAUCHE"," et DROIT pour fixer le carre, et pressez <SPACE>.",""
51997 rem
51998 rem---> Datas packing
51999 rem
52000 data 1,2,4,5,10,20,25,40
52005 data 20,200,40,200,40,400
