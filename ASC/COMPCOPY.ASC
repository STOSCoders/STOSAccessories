10 rem -----------------------------
11 rem     COMPILER COPY UTILITY
12 rem
13 rem         By F. Lionet
14 rem    (c) 1989 Jawx/Mandarin
15 rem -----------------------------
50 dim L(40),D(40),F$(40)
55 DR$="A"
100 key off : if mode=0 then mode 1
105 clw : curs off : hide on 
110 locate 0,0 : square 79,3,3
115 centre "COMPILER COPY UTILITY"
120 locate 0,10 : centre "Please insert COMPILER disk in drive "+DR$+"." : locate 0,20 : centre "Press <G> to begin, <A> or <B> to select drive, any other key to end." : clear key 
125 A$=upper$(inkey$) : if A$="" then 125
130 if A$="A" then DR$="A" : goto 120
135 if A$="B" then DR$="B" : goto 120
140 if A$<>"G" then default : end 
145 locate 0,10 : centre space$(79) : locate 0,20 : centre space$(79)
200 on error goto 900 : dir$="\" : A$=dir first$("*.*",-1) : drive$=DR$ : dir$="\COMPILER" : FILTRE$="*.*" : FLAG=0
205 N=0 : AD=0 : locate 0,10 : centre "Counting files in the folder" : gosub 1000 : if F$="" then error 12
210 on error goto 900 : locate 0,11 : centre str$(N+1) : F$(N)="\COMPILER\"+F$ : open in 1,F$ : L(N)=lof(1) : close 1 : AD=AD+L(N) : inc N
215 gosub 1005 : if F$<>"" then 210
220 on error goto 900 : centre space$(79) : dir$="\" : if dir first$("COMPILER.ACB",-1)<>"" then F$(N)="\COMPILER.ACB" : open in #1,"COMPILER.ACB" : L(N)=lof(1) : close 1 : AD=AD+L(N) : inc N
225 if AD+2000>free then 920
230 erase 10 : reserve as work 10,AD : AD=start(10)
235 on error goto 900 : for X=0 to N-1
240 locate 0,10 : centre space$(79) : centre "Loading "+F$(X)
245 D(X)=AD : bload F$(X),AD : AD=AD+L(X)
250 next X
300 on error goto 900 : centre space$(79)
305 locate 0,10 : centre "Please insert a FORMATED disk into drive "+DR$+"." : locate 0,20 : centre "Press <G> to go on, <A> or <B> to select drive, any other key to abort."
310 clear key 
315 A$=upper$(inkey$) : if A$="" then 315
320 if A$="A" then DR$="A" : goto 305
325 if A$="B" then DR$="B" : goto 305
330 centre space$(79) : locate 0,10 : centre space$(79) : if upper$(A$)<>"G" then default : end 
335 drive$=DR$ : dir$="\" : A$=dir first$("*.*",-1)
400 if dir first$("COMPILER",-1)="" then if dfree>AD-start(10) then locate 0,10 : centre "Creating COMPILER folder" : mk dir "COMPILER" else 910
405 for X=0 to N-1
410 locate 0,10 : centre space$(79) : centre "Saving "+F$(X)
415 bsave F$(X),D(X) to D(X)+L(X)
420 next X
500 locate 0,10 : centre space$(79) : centre "COPY FINISHED" : locate 0,20 : centre "Press any key" : clear key : wait key 
505 goto 300
900 locate 0,10 : centre space$(79) : centre "DISK ERROR" : locate 0,20 : centre "Press any key." : clear key : wait key : centre space$(79)
905 if errl>=300 then resume 300 else resume 100
910 bell : locate 0,10 : centre "Not enough space on this disk!" : locate 0,20 : centre "Press any key." : clear key : wait key : centre space$(79) : goto 300
920 bell : locate 0,10 : centre "OUT OF MEMORY" : print : print : centre "Please erase accessories or loaded programs." : locate 0,20 : centre "Press any key." : clear key : wait key : default : end 
999 end 
1000 on error goto 1050 : A$=dir first$(FILTRE$,-1) : goto 1020
1005 on error goto 1050 : A$=dir first$(FILTRE$,-1) : F=val(mid$(A$,42))
1010 A$=left$(A$,12)-" " : if (A$<>F$ or F<>FLAG) then if A$<>"" then A$=dir next$ : F=val(mid$(A$,42)) : goto 1010
1015 A$=dir next$
1020 F=val(mid$(A$,42)) : F$=left$(A$,12)-" " : if F<>FLAG then 1015
1025 return 
1050 locate 0,10 : centre space$(79) : centre "DISK ERROR" : locate 0,20 : centre "Press any key." : clear key : wait key : pop : resume 100
