1 rem
2 rem Memory monitor
3 rem
4 rem STOS basic accessory
5 rem By F. Lionet & C. Sotiropoulos
6 rem (c) JAWX - MANDARIN
7 rem
10 key off : if mode=0 then mode 1
20 dim S$(24)
30 on error goto 860
40 menu$ (1)=" Stos " : menu$ (1,1)=" Set address " : menu$ (1,2)="-------------" : menu$ (1,2) off : menu$ (1,3)=" Help" : menu$ (1,4)=" Quit "
50 for X=1 to 4 : menu$ (X+1)=" Program"+str$(X)+" "
60 for Y=1 to 15 : A$=str$(Y) : if len(A$)=2 then A$=A$+" "
70 if accnb=0 and X=current then 100
80 if length(X,Y)=0 then menu$ (X+1,Y)=" Bank"+A$+" empty        " : menu$ (X+1,Y) off : goto 120
90 menu$ (X+1,Y)=" Bank"+A$+" at "+hex$(start(X,Y),6)+" " : goto 120
100 if length(Y)=0 then menu$ (X+1,Y)=" Bank"+A$+" empty         " : menu$ (X+1,Y) off : goto 120
110 menu$ (X+1,Y)=" Bank"+A$+" at "+hex$(start(Y),6)+" " : goto 120
120 next Y : next X
130 menu on 6
140 scroll off 
150 AD=logic : gosub 830
160 rem---> Main loop
170 clear key 
180 AD=ADP+YY*16 : on mnbar goto 500,720,730,740,750
190 K$=inkey$ : K=scancode : if K$="" then 180
200 if K$=chr$(13) then 410
210 if asc(K$)=0 or asc(K$)=8 then 250
220 if asc(K$)<32 then 180
230 if XX>78 then wait 5 : bell : goto 170
240 mid$(S$(YY),XX+1)=K$ : locate XX,YY : print K$; : inc XX : goto 170
250 if K=75 then if XX>12 then dec XX : locate XX,YY : goto 170
260 if K=77 then if XX<78 then inc XX : locate XX,YY : goto 170
270 if K=82 then if ADP>16*24 then AD=ADP-16*24 : gosub 830 : goto 170
280 if K=71 then AD=ADP+16*24 : gosub 830 : goto 170
290 if K<>72 then 330
300 if YY>0 then dec YY : locate XX,YY : goto 170
310 if ADP<16 then wait 5 : bell : goto 170
320 ADP=ADP-16 : AD=ADP : gosub 790 : locate 0,YY : scroll down : print A$; : for X=23 to 1 step-1 : S$(X)=S$(X-1) : next X : S$(0)=A$ : locate XX,YY : goto 170
330 if K<>80 then 360
340 if YY<23 then inc YY : locate XX,YY : goto 170
350 ADP=ADP+16 : AD=ADP+16*23 : gosub 790 : scroll up : locate 0,YY : print A$; : for X=0 to 22 : S$(X)=S$(X+1) : next X : S$(23)=A$ : locate XX,YY : goto 170
360 if K=14 then if XX>12 then dec XX : locate XX,YY : print " "; : locate XX,YY : mid$(S$(YY),XX+1)=" " : goto 170
370 if K=97 then gosub 790 : locate 0,YY : print A$ : S$(YY)=A$ : locate XX,YY : goto 170
380 if K=98 then gosub 530 : goto 170
390 goto 170
400 rem---> RETURN
410 if XX>=63 then 440
420 for X=0 to 15 : Y=val("$"+mid$(S$(YY),13+X*3,2)) : if Y<0 or Y>255 then Y=0
430 poke AD+X,Y : next X : goto 470
440 for X=0 to 15 : Y=asc(mid$(S$(YY),64+X,1)) : if Y=254 then 460
450 poke AD+X,Y
460 next X
470 locate 0,YY : gosub 790 : S$(YY)=A$ : print A$;
480 if XX<63 then XX=12 else XX=63
490 goto 340
500 on mnselect gosub 530,510,570,700
510 goto 170
520 rem---> Set address
530 windopen 10,20,10,40,5,7 : inverse on : clw 
540 clear key : put key "$" : locate 1,1 : input "Enter start address (hexa):";A$
550 AD=val(A$) : windel 10 : gosub 830 : return 
560 rem---> Help
570 windopen 10,20,5,40,13,7 : inverse on : clw 
580 locate 0,1 : centre "Move cursor with arrows,"
590 locate 0,2 : centre "<INSERT> displays previous page"
600 locate 0,3 : centre "<HOME> displays next page,"
610 locate 0,4 : centre "<UNDO> erases all changes on line,"
620 locate 0,5 : centre "<HELP> fix address,"
630 locate 0,6 : centre "Press <RETURN> to validate changes."
640 locate 0,7 : centre "DON'T CHANGE SYSTEM ADDRESS!!!"
650 locate 0,9 : centre "Press any key..."
660 clear key : repeat : until inkey$<>""
670 windel 10
680 return 
690 rem---> Quit
700 default : end 
710 rem---> Bank address
720 X=1 : goto 760
730 X=2 : goto 760
740 X=3 : goto 760
750 X=4
760 Y=mnselect : if accnb=0 and X=current then AD=start(Y) else AD=start(X,Y)
770 gosub 830 : goto 170
780 rem---> Print line
790 A$=hex$(AD,6)-"$"+":     " : for X=0 to 15 : A$=A$+(hex$(peek(AD+X),2)-"$")+" " : next X
800 A$=A$+"   " : for X=0 to 15 : Y=peek(AD+X) : if Y<32 then A$=A$+chr$(254) else A$=A$+chr$(Y)
810 next X : AD=AD+16 : return 
820 rem---> Print page
830 ADP=AD : menu freeze : home : for Z=0 to 23 : gosub 790 : S$(Z)=A$ : print A$ : next Z
840 XX=12 : YY=0 : locate XX,YY : menu on : return 
850 rem---> Error correction
860 wait 5 : bell : AD=logic : gosub 830 : resume 170
