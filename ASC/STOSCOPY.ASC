100 rem ******************************************
110 rem * STOSCOPY Version 2.0 by Richard Vanner *
120 rem *       (c) 1988 Mandarin/Jawx           *
130 rem ******************************************
140 FOLD$="STOS" : drive=0
150 cls : centre "STOSCOPY  Version 2.0"
160 print : centre "(c) Mandarin / Jawx 1988" : print : print 
170 print : print "Insert a disc containing the ";FOLD$;" folder, and press a key."
180 wait key 
190 dim FILES(64),FILES$(64),FSIZES(64)
200 dir$="\"+FOLD$
210 SOURCE$=dir$+"\*.*"
220 A$=dir first$(SOURCE$,-1) : A$=dir next$
230 A=0
240 repeat 
250 FILE$=dir next$ : if FILE$="" then A=-1 : goto 270
260 FILES(A)=1 : FILES$(A)=left$(FILE$,12) : FSIZES(A)=val(mid$(FILE$,13,8)) : A=A+1
270 until A=-1
280 A=0
290 if FILES(A)=1 then inc A : goto 290 : else NUMFILES=A
300 rem **********************************************
310 rem * Create a new folder onto the new disc *
320 rem **********************************************
330 print : print "Insert a newly formatted disc into the current drive and press a key." : wait key 
340 on error goto 560
350 mk dir FOLD$ : on error goto 0 : print : print "New ";FOLD$;" folder created." : print : print "Now re-insert the source disc and press a key." : wait key 
360 rem ***********************************************
370 rem * Copy 15 files or less into the memory banks *
380 rem ***********************************************
390 FSTART=0 : FMAX=15
400 NF=FSTART : NUMBANKS=1 : while FILES(NF)=1 and NUMBANKS<FMAX+1
410 on error goto 550 : reserve as work NUMBANKS,FSIZES(NF) : on error goto 0
420 print "Loading : ";FILES$(NF);"- into bank ";NUMBANKS : bload "\"+FOLD$+"\"+FILES$(NF),NUMBANKS
430 inc NF : inc NUMBANKS : wend 
440 rem *************************************
450 rem * Save banks out to new folder *
460 rem *************************************
470 print : print "Insert the destination disc, and press a key" : wait key 
480 NF=0
490 print "Saving : ";FILES$(FSTART);"- from bank ";NF+1 : bsave "\STOS\"+FILES$(FSTART),start(NF+1) to start(NF+1)+FSIZES(FSTART)
500 inc FSTART : inc NF : if FSTART=NUMFILES then goto 530
510 if NF<>FMAX then goto 490
520 for A=1 to 15 : erase A : next A : print : print "Insert the STOS source disc, and press a key." : wait key : FMAX=15 : goto 400
530 print : print "Copy complete."
540 end 
550 FMAX=NUMBANKS-1 : goto 430
560 print : print "I can't open the new folder. Please check the disc!" : end 
