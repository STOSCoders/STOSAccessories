2 rem Font editor
3 rem
4 rem STOS basic accessory
5 rem By F. Lionet & C. Sotiropoulos
6 rem (c) JAWX - MANDARIN
7 rem
49 rem  ---> Default set: system!
50 ENCRE=1
55 MULX=2-divx : MULY=2-divy : MXCX=2/divx : MXCY=16/divy : charcopy mode+1 to 5
60 BAUR=1 : CC=0 : OLDCC=0 : GRXCAR=32 : GRYCAR=368/divy : C$=chr$(252)
65 dim CAR(32),T$(2,40),X(32),Y(32)
70 restore 11000 : for X=0 to 1 : Y=0 : repeat : read A$ : T$(X,Y)=A$ : Y=Y+1 : until A$="" : next X
99 rem  ---> Menus
100 L=10000+language*100 : restore L
105 for X=1 to 5 : read A$ : menu$ (X)=A$ : next X
110 for X=1 to 5 : read N : for Y=1 to N : read A$,Z : menu$ (X,Y)=A$
115 if Z then menu$ (X,Y) on else menu$ (X,Y) off 
120 next Y : next X
125 if mode=0 then menu$ (3,3) off : menu$ (3,9) off : menu$ (3,10) off : menu$ (3,2) off : menu$ (3,8) off : menu$ (2,7) off : BPS=160 : BITPLAN=4
126 if mode=1 then menu$ (3,3) off : menu$ (3,9) off : menu$ (3,10) off : BPS=160 : BITPLAN=2
130 if mode=2 then BPS=80 : BITPLAN=1
135 if accnb=0 then menu$ (2,6) off 
150 menu on 1 : menu freeze 
155 on menu goto 4000,5000,6000,7000,8000
995 gosub 9105 : gosub 9300
1000 on menu on : menu on 
1005 if zone(0)=10 then 2000
1010 if zone(0)=11 then 3000
1015 if zone(0)=12 then 3500
1020 goto 1005
1999 rem---> Fenetre d'edition!
2000 menu freeze : qwindow 1
2050 if zone(0)<>10 then 1000
2055 XX=(x mouse-xgraphic(0))/8 : YY=(y mouse-ygraphic(0))/8 : if XX>=CX*8 or XX<0 or YY>=CY or YY<0 then 2050
2060 locate XX,YY
2065 if mouse key=1 then print C$; : ink ENCRE : plot GRXCAR+XX,GRYCAR+YY : bset 8*CX-XX-1,CAR(YY)
2070 if mouse key=2 then print " "; : ink 0 : plot GRXCAR+XX,GRYCAR+YY : bclr 8*CX-XX-1,CAR(YY)
2075 goto 2050
2999 rem---> Select a character
3000 menu freeze : MK=0 : while MK=0 and zone(0)=11
3005 qwindow 2 : CC=(y mouse-ygraphic(0))/CY*16+(x mouse-xgraphic(0))/8/CX : if CC>223 or CC<0 then CC=OLDCC else if CC<>OLDCC then gosub 9300
3010 MK=mouse key : wend : if zone(0)<>11 then CC=CCC : gosub 9300 : goto 1000
3015 if MK<>1 and CX=1 then for Y=0 to CY-1 : poke AD+Y,CAR(Y) : next Y : gosub 9300 : goto 1000
3020 if MK<>1 and CX=2 then for Y=0 to CY-1 : doke AD+Y*2,CAR(Y) : next Y : gosub 9300 : goto 1000
3025 if CX=1 then for Y=0 to CY-1 : CAR(Y)=peek(AD+Y) : next Y
3030 if CX=2 then for Y=0 to CY-1 : CAR(Y)=deek(AD+Y*2) : next Y
3035 gosub 9400 : CCC=CC : goto 1000
3499 rem---> changement de bordure
3500 menu freeze : BAUR=BAUR+1 : if BAUR>15 then BAUR=1
3505 while mouse key=0 and zone(0)=12 : wend 
3510 if mouse key then qwindow 2 : border BAUR : wait 10
3515 goto 1000
3999 rem---> Informations!
4000 menu freeze : windopen 4,20*MULX,8,40,10 : curs off 
4005 locate 0,1 : centre "FONT GENERATOR" : locate 0,4 : centre "By F. Lionet & C. Sotiropoulos" : locate 0,6 : centre "(c) Jawx / Mandarin"
4010 repeat : until mouse key or inkey$<>""
4015 windel 4 : gosub 9500 : goto 1000
4999 rem---> File
5000 menu freeze : on mnselect gosub 5100,5195,5200,1000,5300,5400
5005 goto 1000
5099 rem---> Load from disk
5100 R$=file select$("*.mbk",T$(language,17),4) : gosub 9500 : if R$="" or instr(R$,".MBK")=0 then bell : return 
5105 on error goto 5190
5110 load R$,5 : if leek(start(5))<>$6071963 then T1=10 : goto 5150
5115 if (deek(start(5)+4)>MXCX) or deek(start(5)+6)>MXCY then T1=11 : goto 5150
5120 NOMFILE$=R$ : gosub 9100 : return 
5150 charcopy mode+1 to 5 : boom : T2=12 : gosub 9800
5155 erase 5 : charcopy mode+1 to 5 : gosub 9100 : return 
5190 boom : T1=9 : T2=12 : gosub 9800 : resume 5155
5194 rem---> Save char set
5195 if NOMFILE$="" then 5200 else R$=NOMFILE$ : goto 5205
5199 rem---> Save as...
5200 R$=file select$("*.mbk",T$(language,18),4) : gosub 9500 : if R$="" or instr(R$,".MBK")=0 then bell : return 
5205 on error goto 5250
5210 save R$,5
5215 return 
5250 boom : T1=9 : T2=0 : gosub 9800 : pop : resume 1000
5300 gosub 9600 : if R=0 then return 
5305 erase 5 : default : end 
5399 rem---> quit and grab
5400 windopen 5,20*MULX,5,40,5,10 : scroll off : centre T$(language,13)
5405 print : print : centre T$(language,14) : gosub 9600
5410 if R=0 then windel 5 : gosub 9400 : goto 1000
5415 default : clear key : put key "BGRAB"+str$(accnb)+",5:erase"+str$(accnb)+",5`" : end 
5999 rem--->Character set
6000 menu freeze : on mnselect gosub 6100,6105,6110,6200,6300,1000,6400,6500,6600,6700
6005 goto 1000
6099 rem--->Copy system set #1,#2,#3
6100 SSYS=1 : goto 6115
6105 SSYS=2 : goto 6115
6110 SSYS=3
6115 gosub 9600 : if R=0 then return 
6120 erase 5 : charcopy SSYS to 5 : gosub 9100 : return 
6199 rem---> Grab from a program
6200 windopen 4,20*MULX,2,40,20,10 : centre T$(language,3) : print 
6205 Z=1 : for X=1 to 4 : for Y=1 to 15 : if accnb=0 and X=current then 6230
6210 if length(X,Y)=0 then 6230
6215 if leek(start(X,Y))<>$6071963 then 6230
6220 TTX=deek(start(X,Y)+4) : TTY=deek(start(X,Y)+6) : if TTY>MXCY or TTX>MXCX then 6230
6225 inverse on : print Z;" "; : inverse off : print "-";T$(language,4);X;", ";T$(language,5);Y;":";TTX*8;" X";TTY : X(Z)=X : Y(Z)=Y : Z=Z+1
6230 next Y : next X
6235 if Z=0 then locate 0,16 : centre T$(language,7) : while mouse key=0 : wend : goto 6250
6240 locate 0,16 : print T$(language,6); : input X : if X(X)=0 then 6250
6245 erase 5 : reserve as set 5,length(X(X),Y(X)) : copy start(X(X),Y(X)),start(X(X),Y(X))+length(X(X),Y(X)) to start(5) : reserve as data 6,1 : erase 6 : gosub 9100 : goto 1000
6250 windel 4 : gosub 9500 : return 
6299 rem---> erase character set
6300 gosub 9600 : if R=0 then return 
6305 gosub 9000 : gosub 9100 : return 
6399 rem---> 8X8
6400 gosub 9600 : if R=0 then return 
6405 erase 5 : reserve as set 5,264+224*8
6410 loke start(5),$6071963 : doke start(5)+4,1 : doke start(5)+6,8
6415 gosub 9000 : reserve as data 6,100 : erase 6 : gosub 9100 : return 
6499 rem---> 16X8
6500 gosub 9600 : if R=0 then return 
6505 erase 5 : reserve as set 5,264+224*16
6510 loke start(5),$6071963 : doke start(5)+4,2 : doke start(5)+6,8
6515 gosub 9000 : reserve as data 6,100 : erase 6 : gosub 9100 : return 
6599 rem---> 8X16
6600 gosub 9600 : if R=0 then return 
6605 erase 5 : reserve as set 5,264+224*16
6610 loke start(5),$6071963 : doke start(5)+4,1 : doke start(5)+6,16
6615 gosub 9000 : reserve as data 6,100 : erase 6 : gosub 9100 : return 
6699 rem---> 16X16
6700 gosub 9600 : if R=0 then return 
6705 erase 5 : reserve as set 5,264+224*16*2
6710 loke start(5),$6071963 : doke start(5)+4,2 : doke start(5)+6,16
6715 gosub 9000 : reserve as data 6,100 : erase 6 : gosub 9100 : return 
6999 rem---> Character edit
7000 menu freeze : while mouse key<>0 : wend 
7005 on mnselect gosub 7100,7200,7300,7400,1000,7500
7010 goto 1000
7099 rem---> Erase
7100 for X=0 to CY-1 : CAR(X)=0 : next X : gosub 9400 : return 
7199 rem---> Inverse
7200 for X=0 to CY-1 : CAR(X)=not(CAR(X)) : next X : gosub 9400 : return 
7299 rem---> Renverser
7300 for X=0 to CY/2-1 : swap CAR(X),CAR(CY-1-X) : next X : gosub 9400 : return 
7399 rem---> Miroir
7400 for Y=0 to CY-1 : for X=0 to CX*8/2-1 : A=btst(X,CAR(Y)) : B=btst(CX*8-1-X,CAR(Y))
7405 if A then bset CX*8-1-X,CAR(Y) else bclr CX*8-1-X,CAR(Y)
7410 if B then bset X,CAR(Y) else bclr X,CAR(Y)
7415 next X : next Y : gosub 9400 : return 
7500 while mouse key : wend : X1=x mouse/5 : Y1=y mouse/5
7505 X2=x mouse/5 : Y2=y mouse/5 : if mouse key then return 
7510 if X2<>X1 then if X2<X1 then 7550 else 7560
7515 if Y2<>Y1 then if Y2<Y1 then 7570 else 7575
7520 gosub 9400 : X1=X2 : Y1=Y2 : goto 7505
7550 if CX=1 then for Y=0 to CY-1 : rol .b 1,CAR(Y) : next Y : goto 7515
7555 for Y=0 to CY-1 : rol .w 1,CAR(Y) : next Y : goto 7515
7560 if CX=1 then for Y=0 to CY-1 : ror .b 1,CAR(Y) : next Y : goto 7515
7565 for Y=0 to CY-1 : ror .w 1,CAR(Y) : next Y : goto 7515
7570 X=CAR(0) : for Y=0 to CY-2 : CAR(Y)=CAR(Y+1) : next Y : CAR(CY-1)=X : goto 7520
7575 X=CAR(CY-1) : for Y=CY-1 to 1 step-1 : CAR(Y)=CAR(Y-1) : next Y : CAR(0)=X : goto 7520
8000 menu freeze : qwindow 1 : NP=0 : TX=0 : TY=0
8020 on mnselect gosub 8050,8190,8230,8230
8030 gosub 8370 : gosub 9400 : gosub 9500 : goto 1000
8040 rem---> Draw line
8050 gosub 8300
8060 ink 1 : IX=GRXCAR : IY=GRYCAR : draw IX+TX,IY+TY to IX+BX,IY+BY : return 
8180 rem---> Draw Box
8190 gosub 8300
8200 ink 1 : IX=GRXCAR : IY=GRYCAR : box IX+TX,IY+TY to IX+BX,IY+BY : return 
8220 rem---> Fill
8230 qwindow 1 : repeat 
8240 K=mouse key : if zone(0)=10 and K=1 then XX=(x mouse-xgraphic(0))/8 : YY=(y mouse-ygraphic(0))/8 else 8240
8245 if XX<0 or YY<0 or XX>=CX*8 or YY>=CY or (TX=XX and TY=YY) then 8240
8250 if K<>1 or XX<=0 or XX>15 or YY<0 or YY>15 then 8240
8260 IX=GRXCAR : IY=GRYCAR : ink 1 : paint IX+XX,IY+YY
8270 until K<>0
8280 return 
8290 rem---> Get dimensions of object
8300 TX=-1 : BX=-1 : TY=-1 : BY=-1 : NP=0 : K=0 : while K<>2 and NP<>2
8310 K=mouse key : if zone(0)=10 and K=1 then XX=(x mouse-xgraphic(0))/8 : YY=(y mouse-ygraphic(0))/8 else 8310
8320 if XX<0 or YY<0 or XX>=CX*8 or YY>=CY or (TX=XX and TY=YY) then 8310
8330 if K=1 and NP<2 then locate XX,YY : print chr$(254) : NP=NP+1 : if NP=1 then TX=XX : TY=YY else BX=XX : BY=YY
8340 K=0
8350 wend : return 
8360 rem---> Read char from screen
8370 PS=(IX/8)*BITPLAN+IY*BPS+physic
8390 for I=0 to CY-1
8400 if CX=2 then CAR(I)=deek(PS) else CAR(I)=peek(PS)
8401 PS=PS+BPS
8410 next I : return 
8420 stop 
8999 rem---> efface tout le jeu de caracteres
9000 CX=deek(start(5)+4) : CY=deek(start(5)+6)
9005 for X=0 to 31 : poke start(5)+8+X,0 : next X
9010 for X=0 to 223 : poke start(5)+40+X,X : next X
9015 for X=start(5)+264 to start(5)+264+160*CX*CY-1 : poke X,0 : next X
9020 for X=X to X+63*CX*CY-1 : poke X,255 : next X
9025 for X=X to X+CX*CY-1 : poke X,0 : next X
9030 return 
9099 rem affiche l'ecran de l'editeur
9100 cls
9105 CX=deek(start(5)+4) : CY=deek(start(5)+6) : reset zone 
9110 if mode<>2 then windopen 1,0,2,CX*8+2,CY+2,1,1 else windopen 1,0,4,CX*8+2,CY+2,1,1
9115 scroll off : curs off : set zone 10,xgraphic(0),ygraphic(0) to xgraphic(CX*8-1)+8,ygraphic(CY-1)+16/divy
9120 if mode=0 then windopen 2,20,6,18,16,1,4
9121 if mode=1 or (mode=2 and CY=16) then if CX=1 then windopen 2,40,2,18,16,1,4 else windopen 2,20,2,18,16,1,4
9122 if mode=2 and CY=8 then if CX=1 then windopen 2,40,4,18,16,1,4 else windopen 2,20,4,18,16,1,4
9125 scroll off : curs off : set zone 11,xgraphic(0),ygraphic(0) to xgraphic(15)+8,ygraphic(13)+CY
9130 set zone 12,xgraphic(0)-8,ygraphic(0)-CY to xgraphic(15)+16,ygraphic(13)+CY*2
9135 windopen 3,0,23,34,2,0 : scroll off : curs off 
9140 gosub 9200 : CC=0 : OLDCC=32 : gosub 9300
9145 for Y=0 to CY-1 : CAR(Y)=peek(AD+Y) : next Y : gosub 9400
9150 return 
9180 erase 5 : charcopy SSYS to 5 : gosub 9100 : return 
9199 rem---> Write all char set
9200 qwindow 2 : home : for X=32 to 255 : print chr$(X); : next X : return 
9299 rem---> Inverse chosen char
9300 qwindow 2 : inverse off : locate OLDCC mod 16,OLDCC/16 : print chr$(OLDCC+32);
9305 inverse on : OLDCC=CC : locate CC mod 16,CC/16 : print chr$(CC+32);
9310 inverse off 
9315 X=peek(start(5)+40+CC) : AD=start(5)+264+X*CY*CX
9320 qwindow 3 : locate 6,0 : print T$(language,0);chr$(CC+32);" / Ascii:";CC+32;"   "
9325 return 
9399 rem---> Affichage ZOOME du caractere
9400 qwindow 3 : locate 6,0 : print T$(language,0);chr$(CC+32);" / Ascii:";CC+32;
9401 qwindow 3 : locate 6,0 : print T$(language,0);chr$(CC+32);" / Ascii:";CC+32;
9405 qwindow 1 : home : for Y=0 to CY-1 : for X=8*CX-1 to 0 step-1 : if btst(X,CAR(Y)) then print C$; : ink ENCRE else print " "; : ink 0
9410 plot GRXCAR+CX*8-X-1,GRYCAR+Y : next X : next Y
9411 ink 1 : box GRXCAR-1,GRYCAR-1 to GRXCAR+8*CX+1,GRYCAR+CY
9415 return 
9499 rem---> reaffiche le caractere en petit!
9500 for Y=0 to CY-1 : for X=8*CX-1 to 0 step-1 : if btst(X,CAR(Y)) then ink ENCRE else ink 0
9505 plot GRXCAR+CX*8-X-1,GRYCAR+Y : next X : next Y
9506 ink 1 : box GRXCAR-1,GRYCAR-1 to GRXCAR+8*CX+1,GRYCAR+CY
9510 return 
9599 rem---> Sure? (Y/N)
9600 windopen 4,11+18*MULX,12,22,4,2 : scroll off : curs off 
9605 centre T$(language,1) : inverse on : print : centre T$(language,2)
9610 set zone 1,xgraphic(0),ygraphic(0) to xgraphic(10),ygraphic(1)+16/divy
9615 set zone 2,xgraphic(11),ygraphic(0) to xgraphic(19)+8,ygraphic(1)+16/divy
9620 repeat : until (zone(0)=1 or zone(0)=2) and mouse key<>0
9625 R=0 : if zone(0)=2 then R=1
9630 windel 4 : gosub 9500 : set zone 1,0,0 to 1,1 : set zone 2,0,0 to 1,1 : return 
9799 rem ---> Dialog box!
9800 windopen 4,20*MULX,10,40,6,8 : centre T$(language,T1) : print : if T2<>0 then centre T$(language,T2)
9805 print : print : centre T$(language,8)
9810 while mouse key=0 : wend : windel 4 : gosub 9400 : return 
9999 rem ---> English menu
10000 data " STOS "," FILE "," SYSTEM "," TOOLS "," DRAW "
10005 data 1,"About FONTS",1
10010 data 6,"Load char set",1,"Save char set",1,"Save as ...",1,"-------------",0,"Quit",1,"Quit and grab",1
10015 data 10,"Copy system set #1",1,"Copy system set #2",1,"Copy system set #3",1,"Grab from program",1,"Erase set",1
10016 data "--------------------",0,"Edit 8X8",1,"Edit 16X8",1,"Edit 8X16",1,"Edit 16X16",1
10020 data 6,"Clear",1,"Inverse",1,"Flip",1,"Mirror",1,"------------",0,"Scroll ",1
10025 data 4,"Line",1,"Box",1,"--------",0,"Fill",1
10099 rem  ---> Menus francais
10100 data " STOS "," DISQUE "," SYSTEME "," OUTILS "," DESSINER "
10105 data 1,"A propos de FONTS",1
10110 data 6,"Charger un jeu",1,"Sauver le jeu",1,"Sauver comme ...",1,"----------------",0,"Quitter",1,"Quitter / GRAB",1
10115 data 10,"Copie jeu systŠme #1",1,"Copie jeu systŠme #2",1,"Copie jeu systŠme #3",1,"Copier d'un pgm",1,"Effacer le jeu",1
10116 data "-------------------",0,"Edition 8X8",1,"Edition 16X8",1,"Edition 8X16",1,"Edition 16X16",1
10120 data 6,"Effacer",1,"Inverser",1,"Renverser",1,"Miroir",1,"---------",0,"Scroller",1
10130 data 4,"Ligne",1,"Boite",1,"--------",0,"Remplir",1
10999 rem---> Messages
11000 data "Character: ","Sure ?","    NO   | |  YES   ","--->CHARACTER SET IN PROGRAMS<---","Program #","Bank #","Type a number: ","Press mouse key..."
11005 data "Press mouse key to go on...","Disk error!","This bank is not a character set!","Character too large !","Loading default character set..."
11010 data "CAUTION! Character set is copied in","memory bank number 5!","Memory banks present on disk:","Enter chosen name :","       LOAD CHARACTER SET         File name must end with .MBK!","       SAVE CHARACTER SET         File name must end with .MBK!",""
11100 data "CaractŠre: ","Certain ?","   NON   | |  OUI   ","->JEUX DE CARACTERES DISPONIBLES<-","Programme #","Banque #","Entrez un chiffre","Pressez une touche de la souris."
11105 data "Pressez une touche de la souris.","Erreur diquette.","Ce n'est pas jeu de caractŠres!","CaractŠres trop grands !","Je charge le jeu par defaut..."
11110 data "ATTENTION! Les caractŠres sont copi‚s","dans la banque num‚ro 5!","Banques pr‚sentes sur le disque:","Entrez le nom choisi ","  CHARGER UN JEU DE CARACTERES   Le nom doit se finir par .MBK!","  SAUVER LE JEU DE CARACTERES    Le nom doit se finir par .MBK!",""
