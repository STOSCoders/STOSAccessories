3 rem            ICONS EDITOR
4 rem
5 rem        Stos Basic accessory
6 rem   By F.Lionet & C. Sotiropoulos
7 rem ---------------------------------
8 rem
10 dim T$(50),I(255),C(16),M(16),U(16)
50 on mode goto 60,65
55 COL=15 : HCAR=8 : NLINE=20 : NCOL=2 : BITPLAN=4 : BPS=160 : CL=6 : goto 70
60 COL=3 : HCAR=8 : NLINE=40 : NCOL=2 : BITPLAN=2 : BPS=160 : CL=2 : goto 70
65 COL=1 : HCAR=16 : NLINE=40 : NCOL=4 : BITPLAN=1 : BPS=80 : CL=0
70 NPAGE=NLINE*NCOL : PARAM=1 : C$=chr$(252)
99 rem---> Initialise menus
100 restore 50000+language*100
105 for X=1 to 4 : read A$ : menu$ (X)=A$ : next X
110 for X=1 to 4 : read N : for Y=1 to N : read A$,Z : menu$ (X,Y)=A$ : if Z=0 then menu$ (X,Y) off 
115 next Y : next X
120 if accnb=0 then menu$ (1,4) off 
125 menu on 4 : menu freeze 
130 on menu goto 6000,7000,8000,9000
199 rem---> Initialise text
200 restore 51000+language*100
205 X=0 : repeat : read T$(X) : inc X : until T$(X-1)=""
299 rem---> Reserves memory bank
300 erase 2 : reserve as data 2,21420+4+2
305 gosub 11000
310 reserve as work 1,1 : erase 1
315 ADW=start(2)+6+84*254+10
900 gosub 10005
905 B=2 : if accnb=0 or length(B,2)=0 then 1000
910 if leek(start(B,2))<>$28091960 then 1000
915 windopen 6,20*(2-divx),7,40,9,6 : inverse on : clw : locate 0,3 : centre T$(21) : wait 75
920 windel 6 : qwindow 5 : inverse off : clw : gosub 11000 : L=(deek(start(B,2)+4))*84 : copy start(B,2),start(B,2)+L to start(2) : reserve as work 1,1 : erase 1
925 HEIGHT=deek(start(2)+4) : doke start(2)+4,256 : gosub 11100 : gosub 10100
930 AD=start(2)+16 : for X=0 to 15 : M(X)=deek(AD+X*4) : C(X)=deek(AD+X*4+2) : next X : gosub 10300
999 rem---> Main loop
1000 gosub 10600
1005 on error goto 0
1010 menu on : on menu on 
1015 on zone(0)-9 goto 2000,3000,4000,4050,5000,5500
1020 goto 1015
1999 rem---> Mouse in ICON window
2000 menu freeze : qwindow 1
2005 gosub 10200 : if XX<0 or YY<0 then 1000
2010 locate XX,YY : if K=1 then print C$; : bset 15-XX,C(YY) else print " " : bclr 15-XX,C(YY)
2015 goto 2005
2025 goto 2005
2999 rem---> Mouse in MASK window
3000 menu freeze : qwindow 2
3001 bell : boom : shoot 
3005 gosub 10200 : if XX<0 or YY<0 then 1000
3010 locate XX,YY : if K=1 then print C$; : bset 15-XX,M(YY) else print " "; : bclr 15-XX,M(YY)
3025 goto 3005
3999 rem---> Previous page
4000 menu freeze : qwindow 4 : locate 0,0 : inverse on : print T$(6);
4005 if zone(0)<>12 then qwindow 4 : inverse off : locate 0,0 : print T$(6); : goto 1010
4010 if mouse key=0 then 4005
4015 if PAGE=0 then 4005
4020 PAGE=PAGE-1 : gosub 10100 : goto 4005
4049 rem---> Next page
4050 menu freeze : qwindow 4 : locate 25,0 : inverse on : print T$(7);
4055 if zone(0)<>13 then qwindow 4 : inverse off : locate 25,0 : print T$(7); : goto 1010
4060 if mouse key=0 then 4055
4065 if mode=0 then if PAGE<6 then 4085
4070 if mode=1 then if PAGE<3 then 4085
4075 if mode=2 then if PAGE<1 then 4085
4080 goto 4055
4085 PAGE=PAGE+1 : gosub 10100 : goto 4055
4999 rem---> Chose / Put icon
5000 menu freeze : qwindow 4 : clw : centre T$(9)+str$(PAGE*NPAGE+NUM+1) : NN=NUM
5005 qwindow 5 : if zone(0)<>14 then I=0 : N=NN : gosub 10500 : gosub 10700 : if NUMPAGE=PAGE then N=NUM : I=1 : gosub 10500 : goto 1000 else 1000
5010 K=mouse key : XX=(x mouse-xgraphic(0))/16 : YY=(y mouse-ygraphic(0))/16 : if XX>=NLINE or YY>=NCOL then 5005
5015 N=YY*NLINE+XX : if (K=0 and N=NN) or PAGE*NPAGE+N+1>254 then 5005
5020 swap N,NN : I=0 : gosub 10500 : N=NN : I=1 : gosub 10500 : qwindow 4 : inverse off : clw : centre T$(9)+str$(PAGE*NPAGE+N+1)
5025 if K=0 or X>254 then 5005
5030 AD=start(1)+6+84*(PAGE*NPAGE+N)+10 : if K=1 then for X=0 to 15 : U(X)=C(X) : M(X)=deek(AD+X*4) : C(X)=deek(AD+X*4+2) : next X : gosub 10300 : gosub 10600 : NUM=N : NUMPAGE=PAGE
5035 if K=2 then for X=0 to 15 : doke AD+X*4,M(X) : doke AD+X*4+2,C(X) : next X : I=1 : gosub 10500 : HEIGHT=max(HEIGHT,PAGE*NPAGE+N+1)
5040 goto 5005
5499 rem---> UNDO
5500 menu freeze : qwindow 8
5510 while mouse key=0 and zone(0)=15 : wend : if mouse key=0 then goto 1010
5520 for X=0 to 15 : T=U(X) : C(X)=U(X) : U(X)=T : next : gosub 10300 : gosub 10600 : NUM=N : NUMPAGE=PAGE
5530 goto 1010
5999 rem---> STOS
6000 menu freeze : on mnselect gosub 6100,6005,6200,6300
6005 goto 1000
6099 rem---> About icons generator
6100 windopen 6,20*(2-divx),5,40,15,9 : curs off : set paint 3,13,0 : bar xgraphic(0),ygraphic(0) to xgraphic(37)+7,ygraphic(12)+HCAR
6105 locate 0,2 : centre T$(0) : locate 0,6 : centre T$(1) : locate 0,8 : centre T$(2) : locate 0,11 : centre T$(3)
6110 while mouse key=0 : wend : windel 6 : while mouse key : wend 
6115 return 
6199 rem---> Quit
6200 gosub 10800 : if R=0 then bell : return 
6205 erase 2 : default : end 
6299 rem---> Quit and grab
6300 gosub 10800 : if R=0 then bell : return 
6305 hide : wait 5 : copy start(2),start(2)+length(2) to back : L=6+HEIGHT*84+84
6310 erase 2 : reserve as data 2,L : copy back,back+L to start(2) : doke start(2)+4,HEIGHT
6315 default : put key "bgrab"+str$(accnb)+",2:erase"+str$(accnb)+",2`"
6320 end 
6999 rem---> File
7000 menu freeze : on mnselect gosub 7100,7195,7200,7005,7300,7005,7400
7005 goto 1000
7099 rem---> Load a bank
7100 on error goto 7145
7105 R$=file select$("*.MBK",T$(11),3) : if R$="" or instr(R$,".MBK")=0 then bell : return 
7110 open in #1,R$ : X=lof(1) : close : if X>$5440 then T=14 : goto 7150
7115 reserve as data 1,255 : gosub 11000 : bload R$,start(2)-18 : if leek(start(2))<>$28091960 then T=14 : goto 7150
7120 erase 1
7125 NOMFILE$=R$ : HEIGHT=deek(start(2)+4) : doke start(2)+4,256 : gosub 11100 : gosub 10100
7130 AD=start(2)+16 : for X=0 to 15 : M(X)=deek(AD+X*4) : C(X)=deek(AD+X*4+2) : next X : gosub 10300 : return 
7145 T=15 : resume 7150
7150 erase 1 : cls : boom : gosub 10900 : pop : goto 300
7194 rem---> Save bank
7195 if NOMFILE$="" then 7200 else R$=NOMFILE$ : goto 7205
7199 rem---> Save as...
7200 R$=file select$("*.MBK",T$(12),3) : if R$="" or instr(R$,".MBK")=0 then bell : return 
7205 on error goto 7245
7210 hide : wait 5 : copy start(2),start(2)+length(2) to back : L=6+HEIGHT*84+84
7215 erase 2 : reserve as data 2,L : copy back,back+L to start(2) : doke start(2)+4,HEIGHT : save R$,2
7220 erase 2 : reserve as data 2,21420+6 : copy back,back+length(2) to start(2) : reserve as data 1,1 : erase 1
7225 screen copy logic to back : show 
7230 return 
7245 gosub 7220 : boom : T=15 : gosub 10900 : resume 7230
7299 rem---> Grab from program
7300 gosub 10800 : if R=0 then bell : return 
7305 windopen 6,4+20*(2-divx),8,30,10,6 : centre T$(16) : print : print 
7310 Z=0 : for B=1 to 4 : if accnb=0 and B=current then 7330
7315 if length(B,2)=0 then 7330
7320 if leek(start(B,2))<>$28091960 then 7330
7325 bset B,Z : print T$(17);B;T$(18);length(B,2)
7330 next B
7335 if Z=0 then windel 6 : T=19 : gosub 10900 : return 
7340 locate 0,6 : print T$(20); : input B : if B<1 or B>4 then 7355
7345 if btst(B,Z)=0 then 7355
7350 windel 6 : qwindow 5 : inverse off : clw : gosub 11000 : L=(deek(start(B,2)+4))*84 : copy start(B,2),start(B,2)+L to start(2) : reserve as work 1,1 : erase 1 : R$="" : goto 7125
7355 windel 6 : goto 7405
7399 rem ---> Erase icons bank
7400 gosub 10800 : if R=0 then bell : return 
7405 gosub 11000 : gosub 10100 : gosub 10400 : gosub 10300
7410 return 
7999 rem ---> tools
8000 menu freeze : on mnselect gosub 8100,8200,8300,8400,8005,8500
8005 goto 1000
8099 rem ---> Clear
8100 gosub 10800 : if R=0 then bell : return 
8105 gosub 10400 : gosub 10300 : return 
8199 rem ---> Inverse
8200 for Y=0 to 15 : C(Y)=not(C(Y)) and $FFFF : next Y
8205 gosub 10300 : return 
8299 rem ---> Flip
8300 for Y=0 to 7 : swap C(Y),C(15-Y) : next Y
8305 gosub 10300 : return 
8399 rem ---> Mirror
8400 for Y=0 to 15 : Z=0 : for X=0 to 15 : if btst(X,C(Y)) then bset 15-X,Z
8405 next X : C(Y)=Z : next Y
8410 gosub 10300 : return 
8499 rem ---> scrolls
8500 while mouse key : wend : X1=x mouse/5 : Y1=y mouse/5
8505 X2=x mouse/5 : Y2=y mouse/5 : if mouse key then return 
8510 if X2<>X1 then if X2<X1 then 8550 else 8560
8515 if Y2<>Y1 then if Y2<Y1 then 8570 else 8580
8520 gosub 10300 : gosub 10600 : X1=X2 : Y1=Y2 : goto 8505
8550 for Y=0 to 15 : rol 1,C(Y) : next Y
8552 for X=0 to 14 : if btst(X+1,C(Y)) then bset X,Z
8554 next X
8555 goto 8515
8560 for Y=0 to 15 : ror 1,C(Y) : next Y
8562 for X=1 to 15 : if btst(X-1,C(Y)) then bset X,Z
8564 next X
8565 goto 8515
8570 Z=C(0) : for Y=1 to 15 : C(Y-1)=C(Y) : next Y : C(15)=Z
8575 goto 8520
8580 Z=C(15) : for Y=14 to 0 step-1 : C(Y+1)=C(Y) : next Y : C(0)=Z
8585 goto 8520
8999 rem---> Draw Menu
9000 menu freeze : qwindow 1 : NP=0 : TX=0 : TY=0
9005 WTX=xgraphic(0)+1 : WTY=ygraphic(0)+1 : WBX=xgraphic(15) : WBY=ygraphic(15)
9010 on mnselect gosub 9100,9300,9400,9500,9500
9015 gosub 9900 : gosub 10300 : goto 1000
9099 rem---> Draw line
9100 gosub 9600
9110 ink 1 : qwindow 3 : IX=xgraphic(0) : IY=ygraphic(1) : MX=IX : MY=ygraphic(4) : draw IX+TX,IY+TY to IX+BX,IY+BY : ink 0 : draw MX+TX,MY+TY to MX+BX,MY+BY : return 
9299 rem---> Draw ellipse
9300 gosub 9600
9310 qwindow 3 : IX=xgraphic(0) : IY=ygraphic(1) : MX=IX : MY=ygraphic(4)
9320 DX=abs(BX-TX) : DY=abs(BY-TY) : XC=DX/2+min(TX,BX) : YC=DY/2+min(TY,BY) : R1=DX/2 : R2=DY/2+1
9330 ink 1 : earc IX+XC,IY+YC,R1,R2,0,3600 : ink 0 : earc MX+XC,MY+YC,R1,R2,0,3600 : return 
9399 rem---> Draw Box
9400 gosub 9600
9410 qwindow 3 : IX=xgraphic(0) : IY=ygraphic(1) : MX=IX : MY=ygraphic(4)
9430 ink 1 : box IX+TX,IY+TY to IX+BX,IY+BY : ink 0 : box MX+TX,MY+TY to MX+BX,MY+BY : return 
9499 rem---> Fill
9500 qwindow 1 : repeat 
9510 if zone(0)=10 or zone(0)=11 then gosub 10200 else 9510
9520 if K<>1 or XX<=0 or XX>15 or YY<0 or YY>15 then 9510
9525 qwindow 3 : IX=xgraphic(0) : IY=ygraphic(1) : MX=IX : MY=ygraphic(4) : ink 1 : paint IX+XX,IY+YY : ink 0 : paint MX+XX,MY+YY
9530 until K<>0
9540 return 
9599 rem---> Get dimensions of object
9600 TX=-1 : BX=-1 : TY=-1 : BY=-1 : NP=0 : K=0 : while K<>2 and NP<>2
9610 if zone(0)=10 or zone(0)=11 then gosub 10200 else 9610
9620 if XX<0 or YY<0 or (TX=XX and TY=YY) then 9610
9630 if K=1 and NP<2 then locate XX,YY : print chr$(254) : NP=NP+1 : if NP=1 then TX=XX : TY=YY else BX=XX : BY=YY
9635 K=0
9640 wend : return 
9899 rem---> Read icon from screen
9900 PS=(IX/8)*BITPLAN+IY*BPS+physic
9905 qwindow 1
9910 for I=0 to 15
9920 C(I)=deek(PS) : PS=PS+BPS
9930 next I : return 
9998 stop 
9999 rem---> Displays screen
10000 cls
10005 reset zone : on mode goto 10015,10020
10010 windopen 1,5,2,18,18,15 : windopen 3,25,2,4,10,1 : windopen 4,0,20,40,1,0 : windopen 5,0,21,40,4,0 : windopen 8,24,13,6,3,1,1 : goto 10025
10015 windopen 1,20,2,18,18,15 : windopen 3,51,2,4,9,1 : windopen 4,20,20,40,1,0 : windopen 5,0,21,80,4,0 : windopen 8,49,13,6,3,1,1 : goto 10025
10020 windopen 1,20,10,18,18,1,1 : windopen 3,51,5,4,9,1 : windopen 4,20,20,40,1,0 : windopen 5,0,21,80,4,0 : windopen 8,42,13,6,3,1,1
10025 scroll off : print "UNDO"; : qwindow 1 : curs off : scroll off : set zone 10,xgraphic(0),ygraphic(0) to xgraphic(15)+7,ygraphic(15)+7 : title T$(4)
10030 qwindow 3 : curs off : scroll off 
10035 qwindow 4 : curs off : scroll off : set zone 12,xgraphic(0),ygraphic(0) to xgraphic(16),ygraphic(0)+HCAR-1 : set zone 13,xgraphic(25),ygraphic(0) to xgraphic(39)+7,ygraphic(0)+HCAR-1
10040 gosub 10700
10045 qwindow 5 : curs off : scroll off : set zone 14,xgraphic(0),ygraphic(0) to 640/divx-1,400/divy-1
10046 qwindow 8 : curs off : scroll off : set zone 15,xgraphic(0),ygraphic(0) to xgraphic(3)+8,ygraphic(0)+8
10050 gosub 10100 : gosub 10400 : gosub 10300
10055 return 
10099 rem---> Displays icons
10100 gosub 10700
10105 qwindow 5 : inverse off : clw : on mode goto 10120,10130
10110 for Y=0 to 1 : for X=0 to 19 : locate X*2,Y*2 : Z=PAGE*40+Y*20+X+1 : if Z<255 then print icon$(Z);
10115 next X : next Y : goto 10140
10120 for Y=0 to 1 : for X=0 to 39 : locate X*2,Y*2 : Z=PAGE*80+Y*40+X+1 : if Z<255 then print icon$(Z);
10125 next X : next Y : goto 10140
10130 for Y=0 to 3 : for X=0 to 39 : locate X*2,Y : Z=PAGE*160+Y*40+X+1 : if Z<255 then print icon$(Z);
10135 next X : next Y
10140 if NUMPAGE=PAGE then N=NUM : I=1 : gosub 10500
10145 return 
10199 rem---> Calculate X and Y coordinates in windows
10200 XX=(x mouse-xgraphic(0))/8 : if XX>15 then XX=-1
10205 YY=(y mouse-ygraphic(0))/8 : if YY>15 then YY=-1
10210 K=mouse key : if K or XX<0 or YY<0 then return 
10215 goto 10200
10299 rem---> Display new icon
10300 qwindow 1 : home 
10305 for Y=0 to 15 : for X=0 to 15 : if btst(15-X,C(Y)) then print C$; : else print " ";
10310 next X : next Y
10315 return 
10399 rem---> New icon
10400 for X=0 to 15 : C(X)=0 : M(X)=$FFFF : next X : return 
10499 rem---> displays icon #N
10500 if N<0 then N=0
10501 qwindow 5 : X=(N mod NLINE)*2 : Y=N/NLINE : inverse off 
10505 if mode=0 or mode=1 then locate X,Y*2 else locate X,Y
10510 if I then inverse on else inverse off 
10515 X=PAGE*NPAGE+N+1 : if X>=0 and X<255 then print icon$(X);
10520 return 
10599 rem---> displays icon in background
10600 for X=0 to 15 : doke ADW+X*4,M(X) : doke ADW+X*4+2,C(X) : next X
10605 qwindow 3 : clw : inverse off : locate 0,1 : print icon$(255); : inverse on : locate 0,4 : print icon$(255); : inverse off 
10606 ink 1 : for I=0 to 1 : LX=xgraphic(0) : LY=ygraphic(1+I*3) : MX=(xgraphic(1)-xgraphic(0))*2+LX : MY=ygraphic(3+I*3) : box LX-1,LY-2 to MX,MY+1 : next I : return 
10699 rem---> Previous/page/next
10700 qwindow 4 : inverse off : locate 0,0 : print T$(6); : locate 25,0 : print T$(7);
10705 A$=T$(8) : mid$(A$,7)=str$(PAGE+1) : centre A$ : return 
10799 rem---> Confirm
10800 paper 0 : windopen 7,4+20*(2-divx),12,30,3,10 : scroll off : curs off : inverse on : print T$(10)
10805 set zone 1,xgraphic(0),ygraphic(0) to 320/divx,ygraphic(0)+HCAR : set zone 2,320/divx,ygraphic(0) to xgraphic(27)+7,ygraphic(0)+HCAR
10810 repeat : until mouse key and (zone(0)=1 or zone(0)=2)
10815 if zone(0)=2 then R=1 else R=0
10820 windel 7 : set zone 1,0,0 to 1,1 : set zone 2,0,0 to 1,1 : while mouse key : wend : return 
10899 rem---> Alert
10900 windopen 6,20*(2-divx),8,40,7,10 : scroll off : curs off : locate 0,1 : centre T$(T) : locate 0,3 : centre T$(13)
10905 repeat : until mouse key : windel 6 : while mouse key : wend 
10910 return 
10999 rem---> Erases bank
11000 loke start(2),$28091960 : doke start(2)+4,256 : fill start(2)+6 to start(2)+length(2),0
11005 for X=start(2)+6 to start(2)+6+255*84 step 84 : doke X,8 : fill X+10 to X+74,$FFFF0000 : next X
11010 NUM=0 : PAGE=0 : NUMPAGE=0 : HEIGHT=0 : return 
11099 rem---> Erases bank up to the end
11100 fill start(2)+6+HEIGHT*84 to start(2)+255*84+6,0
11105 for X=start(2)+6+HEIGHT*84 to start(2)+6+255*84 step 84 : doke X,8 : fill X+10 to X+74,$FFFF0000 : next X
11110 return 
49999 rem ---> English menus
50000 data " STOS "," FILE "," TOOLS "," DRAW "
50005 data 4," ICONS !",1,"---------------",0," Quit",1," Quit and grab",1
50010 data 7," Load icon bank",1," Save icon bank",1," Save as...",1,"-------------------",0," Grab from program ",1,"-------------------",0," Erase bank",1
50015 data 6," Clear",1," Inverse",1," Flip",1," Mirror",1,"----------------",0," Scroll",1
50020 data 5,"Line",1,"Ellipse",1,"Box",1,"--------",0,"Fill",1
50099 rem ---> Menu Francais
50100 data " STOS "," BANQUE "," ICONE "," DRAW "
50105 data 4," ICONES !",1,"--------------------",0," Quitter",1," Quitter/r‚cup‚rer",1
50110 data 7," Charger une banque",1," Sauver la banque",1," Sauver comme ...",1,"--------------------",0," Copier d'un prg",1,"--------------------",0," Effacer la banque",1
50120 data 6," Effacer",1," Inverser",1," Renverser",1," Miroir",1,"-----------",0," Scroller",1
50130 data 5,"Ligne",1,"Ellipse",1,"Boite",1,"--------",0,"Remplir",1
50999 rem---> English messages
51000 data "ICONS EDITOR","By F. Lionet","& C. Sotiropoulos","(c) Jawx/Database 1988"
51005 data " icon "," mask "," previous ","       next   ","Page:  1   ","Icon #","  ABANDON    ::    CONFIRM  "
51010 data "         LOAD ICON BANK           File name must end with .MBK!","         SAVE ICON BANK           File name must end with .MBK!","Press mouse key to go on.","This bank is not an icon bank!","DISK ERROR!"
51015 data "Icons present in memory:"," Program #",", length:","No icons present in memory!"," Input program number: ","Grabbing bank from program #1.",""
51099 rem---> Messages Fran‡ais
51100 data "EDITEUR D'ICONES","Par F. Lionet","& C. Sotiropoulos","(c) Jawx/Database 1988"
51105 data " ic“ne "," masque ","pr‚c‚dente","    suivante  ","Page:  1   ","Ic“ne  ","  ANNULER    ::  CONFIRMER  "
51110 data "       CHARGER DES ICONES        Le nom doit se finir par .MBK!","       SAUVER DES ICONES         Le nom doit se finir par .MBK!","Pressez une touche de la souris.","Cette banque ne contient pas d'ic“ne!","ERREUR DISQUE!"
51115 data "Ic“nes en m‚moire:"," Programme #",", longueur:","Pas d'ic“ne en m‚moire!"," Entrez le num‚ro du prg:","Je copie la banque du programme #1.",""
